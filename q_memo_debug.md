
# debug

## print

`0N!`を付けるとprintデバッグできる。

```
q)f:{0N!a:x*x; b:y*y; a+b}
q)hoge:f[3;4]
9
```

## `break`によるデバッグ

q言語では、未定義な変数を評価しようとすると、その名前の例外が投げられ、デバッグプロンプトに入る。
この性質を利用して、適当な未定義の名前（慣例として`break`を使う）を入れるとブレークポイントとして利用できる

以下のコードは`break`によるデバッグの例である。
もちろん、`break`という名前のグローバル変数やローカル変数が未定義であることが前提である。

```
q)f:{a:x*x; b:y*y; break; a+b}
q)hoge:f[3;4]
'break
  [1]  f:{a:x*x; b:y*y; break; a+b}
                        ^
q))show a
9
q))show b
16
q)):0
q)show hoge
25
```

`q))`となっている行がデバッグプロンプトである。

デバッグプロンプトではそのスコープのローカル変数を操作することができる。
デバッグプロンプトは`\`で終了することができるが、上の例のように`:`を使って、戻り値を与えて処理を続行することもできる。
バッククォート`` ` ``を使うことで１つ上のスコープへ上がる、
また、ドット`.`を使うことで１つ下のスコープへ下がることができる。

```
q)f:{a:x*x; b:y*y; break; a+b}
q)g:{c:z*z;f[x;y]+c}
q)g[3;4;5]
'break
  [2]  f:{a:x*x; b:y*y; break; a+b}
                        ^
q))`
  [1]  g:{c:z*z;f[x;y]+c}
                ^
q))show c
25
q)).
  [2]  f:{a:x*x; b:y*y; break; a+b}
                        ^
q))show a
9
```

また、`.Q.bt[]`でバックトレースを取得できる。

```
q)).Q.bt[]
  [4]  .Q.bt[]
       ^
  [3]  (.Q.dbg)

  [2]  f:{a:x*x; b:y*y; break; a+b}
                        ^
  [1]  g:{c:z*z;f[x;y]+c}
                ^
  [0]  g[3;4;5]
```

## 強制的にデバッグプロンプトに入る方法

エラーの原因がほかのプロセスであったり、コンパイルされたライブラリであったりするとき、失敗してもデバッグプロンプトに入らないことがある。
強制的にデバッグプロンプトに入るためには`\e 1`と入力すればよい。

以下のコードは5秒ごとに`{0 mod `a}`を実行しようとしている。
このままでは`'type`が表示されるだけで、デバッグプロンプトに入ることがないが、
`\e 1`を入力することで強制的にデバッグプロンプトに入ることができる。

```
q)\t 5000
q).z.ts:{0 mod `a}
q)'type
'type
'type
\e 1
q)k'type
  [0]  .z.ts:{0 mod `a}
                ^
q))
```

